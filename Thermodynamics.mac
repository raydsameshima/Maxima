/* 
A note on Thermodynamics (H. B. Callen) 
Ray D. Sameshima 
*/
kill(all)$
"Chapter 1: Basic Concepts and Postulates"$
"Postulate 1. 
 There exists equilibrium states of simple systems that,
 macroscopically, are characterized completely by the internal energy U,
 the volume V, and the mole numbers N1, N2 ... Nr of the chemical
 components."$

"Postulate 2.
 There exists the entropy function of the extensive parameters of any
 composite system, defined for all equilibrium states and having the 
 following property.
 The values assumed by the extensive parameters in the absence of an 
 internal constraint are those that maximize the entropy over the manifold
 of constrained equilibrium states."$

"Postulate 3.
 The entropy of a composite system is additive over the constituent 
 subsystem.
   S(U,V,N) = N*S(U/N, V/N, 1)
 The entropy is continuous and differentiable and is a monotonically 
 increasing function of the energy."$

"Postulate 4. (Nernst postulate or third law os TD)
 The entropy of any system vanishes in the state for which
   diff(U,S) = 0
 that is, at the zero of temperature."$
"Problem 1.9"$ 
assume(U>0,V>0,N>0, S>0);
S[a](U,V,N)
  := (U*V*N)^(1/3);
is(equal(S[a](U,V,N), N*S[a](U/N, V/N, 1)));
define(U[a](S,V,N), ev(U, solve(S[a](U,V,N) = S, U)));
solve(diff(U[a](S,V,N),S) = 0, S);
"Thus this entropy function Sa satisfies both Postulate 3 and 4."$

S[b](U,V,N)
  := (N*U/V)^(2/3);
is(equal(S[b](U,V,N), N*S[b](U/N, V/N, 1)));
N*S[b](U/N, V/N, 1);
"Sb does not satisfy Postulate 3, the additivity, but"$
define(U[b](S,V,N), ev(U, solve(S[b](U,V,N) = S, U)));
solve(diff(U[b](S,V,N),S) = 0, S);
"Sb satisfies 4th postulate."$

S[c](U,V,N)
  := (N*U - V^2)^(1/2);
is(equal(S[c](U,V,N), N*S[c](U/N, V/N, 1)));
define(U[c](S,V,N), ev(U, solve(S[c](U,V,N) = S, U)));
solve(diff(U[c](S,V,N),S) = 0, S);
"Thus this entropy function Sc satisfies both Postulate 3 and 4."$

S[d](U,V,N)
  := V^3/(N*U);
is(equal(S[d](U,V,N), N*S[d](U/N, V/N, 1)));
define(U[d](S,V,N), ev(U, solve(S[d](U,V,N) = S, U)));
solve( diff(U[d](S,V,N),S)= 0, S);
define(T(S,V,N), diff(U[d](S,V,N),S));
limit(T(S,V,N),S,inf);
"Sd does not satisfy Postulate 4."$

S[e](U,V,N)
  := (N^2*V*U^2)^(1/5);
is(equal(S[e](U,V,N), N*S[e](U/N, V/N, 1)));
define(U[e](S,V,N), ev(U, solve(S[e](U,V,N) = S, U)));
solve(diff(U[e](S,V,N),S) = 0, S);
"Se is a true entropy function."$

S[f](U,V,N)
  := N*log(U*V/N^2);
is(equal(S[f](U,V,N), N*S[f](U/N, V/N, 1)));
define(U[f](S,V,N), ev(U, solve(S[f](U,V,N) = S, U)));
solve(diff(U[f](S,V,N),S) = 0, S);
define(T(S,V,N), diff(U[f](S,V,N),S));
"Sf does not follow 4th postulate."$

S[g](U,V,N)
  := (N*U)^(1/2) * exp(-V^2/N^2);
is(equal(S[g](U,V,N), N*S[g](U/N, V/N, 1)));
define(U[g](S,V,N), ev(U, solve(S[g](U,V,N) = S, U)));
solve(diff(U[g](S,V,N),S) = 0, S);
"Sg is an entropy function."$

S[h](U,V,N)
  := (N*U)^(1/2) * exp(-U*V/N);
is(equal(S[h](U,V,N), N*S[h](U/N, V/N, 1)));
N*S[h](U/N, V/N, 1);
define(U[h](pS,V,N), ev(U, solve(S[h](U,V,N) = S, U)));
solve(diff(U[h](S,V,N),S) = 0, S);
"Sh can not be an entropy."$
"From now, we consider U:"$

U[i](S,V,N)
  := S^2/V * exp(S/N);
solve(diff(U[i](S,V,N),S) = 0, S);
"That is, Ui satisfies 4th postulate, since we've assumed N>0, we have T=0 if S=0."$
is(equal(U[i](S,V,N), N*U[i](S/N,V/N,1)));
"Additivity is also ok."$

U[j](S,V,N)
  := N*V*(1 + S/N)*exp(-S/N);
solve(diff(U[j](S,V,N),S) = 0, S);
is(equal(U[j](S,V,N), N*U[j](S/N,V/N,1)));
N*U[j](S/N,V/N,1);
"Sammery; Sa, Sc, Se, Sg, and Ui are physically acceptable."$

"Chapter 2 The Conditions of Equilibrium"$
" Problem 2.2-1"$
"Recall U[a] from chapter 1:"$
U[a]; /* S^3/(N*V) */
S[a];
"The three equations of state:"$
define(T(S,V,N),   diff(U[a](S,V,N),S));
define(P(S,V,N), - diff(U[a](S,V,N),V));
define(mu(S,V,N),  diff(U[a](S,V,N),N));
"By solving T=T(S,V,N) for S, we can evaluate mu as afunction of T,V,N:"$
sol: solve(T(S,V,N)=T, S);
at(mu(S,V,N), sol[2]);

"Problem 2.2-4"$
U[c];
define(T(S,V,N),   diff(U[c](S,V,N),S));
define(P(S,V,N), - diff(U[c](S,V,N),V));
define(mu(S,V,N),  diff(U[c](S,V,N),N));
expand(mu(S,V,N)/T(S,V,N));
expand(P(S,V,N)/T(S,V,N));
"By solving T=T(S,V,N) for S, we can evaluate mu as afunction of T,V,N:"$
sol: solve(T(S,V,N)=T, S);
at(mu(S,V,N), sol);

"Problem 2.2-6"$
U[i];
define(T(S,V,N),   diff(U[i](S,V,N),S));
define(P(S,V,N), - diff(U[i](S,V,N),V));
define(mu(S,V,N),  diff(U[i](S,V,N),N));
factor(expand(T(S,V,N)/mu(S,V,N)));
factor(expand(mu(S,V,N)/P(S,V,N)));

"Problem 2.3-1"$
U[e];
define(T(S,V,N),   diff(U[e](S,V,N),S));
define(P(S,V,N), - diff(U[e](S,V,N),V));
define(mu(S,V,N),  diff(U[e](S,V,N),N));
"By solving T=T(S,V,N) for S, we can evaluate mu as afunction of T,V,N:"$
assume(T>0);
sol: solve(T(S,V,N)=T, S);
at(mu(S,V,N), sol[3]);

"Problem 2.3-3"$
U[g];
define(T(S,V,N),   diff(U[g](S,V,N),S));
define(P(S,V,N), - diff(U[g](S,V,N),V));
define(mu(S,V,N),  diff(U[g](S,V,N),N));
sol: solve(T(S,V,N)=T, S);
at(mu(S,V,N), sol);

"Chapter 3 Some Formal Relationships"$
"Problem 3.1"$
S[a];
U[a];
T:  diff(U[a](S,V,N),S);
P:  - diff(U[a](S,V,N),V);
mu: diff(U[a](S,V,N),N);
is(equal(T*S - P*V + mu*N, U[a](S,V,N)));

S[c];
U[c];
T:  diff(U[c](S,V,N),S);
P:  - diff(U[c](S,V,N),V);
mu: diff(U[c](S,V,N),N);
is(equal(T*S - P*V + mu*N, U[c](S,V,N)));

S[e];
U[e];
T:  diff(U[e](S,V,N),S);
P:  - diff(U[e](S,V,N),V);
mu: diff(U[e](S,V,N),N);
is(equal(T*S - P*V + mu*N, U[e](S,V,N)));

S[g];
U[g];
T:  diff(U[g](S,V,N),S);
P:  - diff(U[g](S,V,N),V);
mu: diff(U[g](S,V,N),N);
is(equal(T*S - P*V + mu*N, U[g](S,V,N)));

S[i];
U[i];
T:  diff(U[i](S,V,N),S);
P:  - diff(U[i](S,V,N),V);
mu: diff(U[i](S,V,N),N);
is(equal(T*S - P*V + mu*N, U[i](S,V,N)));


