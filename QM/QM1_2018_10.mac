/*
QM1_2018_10.mac
2018 QM 1. Homework 10
Ray D. Sameshima
*/
kill(all)$
load("SHO.mac")$

"Question 1 c) Define a state "$
declare(N, [scalar, real]);
s: N*(3*ket(0) + sqrt(3)*ket(1) + 2*ket(3));
sd: conjugate(s);
"The normalization condition should be:"$
sol: solve( sd. s = 1, N);
"Therefore, we have"$
s: at(s, sol[2]);
sd: at(sd, sol[2]);
expand(sd . s);
[expand(bra(0) . s)^2, expand(bra(1) . s)^2, expand(bra(3) . s)^2];
sum(%[i],i,1,length(%));

"d) x and p in energy eigen space."$ 
declare([m,w,h],[scalar, real]);
sol: factor(solve([L=1/sqrt(2*m*w*h)*(p-%i*m*w*x), R=1/sqrt(2*m*w*h)*(p+%i*m*w*x)],[x,p]));
x: ev(x, sol);
p: ev(p, sol);
"So x and p are essentially (R-L) and (R+L) "$

"To see the non-zero elements of x, and x^^2: "$
factor( expand( bra(i) . x . ket(j)));
factor( expand( bra(i) . x^^2 . ket(j)));

"The matrix representations are"$
mat_rep(x,4);
mat_rep(x^^2,4);

"Using indices"$
bra(i) . (R-L) . ket(j), expand;
bra(i) . (R-L)^^2 . ket(j), expand;

"Since x is essentially R-L,"$
mat_rep((R-L), 4);
mat_rep((R-L)^^2, 4);

"e) <x>_s and <x^2>_s in t=0:"$
x;
x^^2;
expand(sd . x . s);
expand(sd . expand(x^^2 . s));

"Let us consider time evolution. "$
declare(U, nonscalar);
declare([t], [scalar, real]);

tellsimp( U(t) . ket(i), exp(-%i*t*w*(i+1/2)) * ket(i));
tellsimp( bra(i) . U(t), conjugate( U(t) . ket(i)));

"f) The state in t and the expectation value for x are"$
s(t) := expand(U(t) . s);
exp_x: expand(demoivre(expand(conjugate(s(t)) . x . s(t))));

exp_x2: expand(demoivre(expand( conjugate(s(t)) . expand(x^^2 . s(t)))));
trigsimp(exp_x2 - exp_x^2);

"Question 2, solutions of wave equation."$ 
kill(h,m,k,x,rho);
declare([h,m,k,x,rho],[scalar, real]);
declare([A,B,C,D],[scalar, complex]);
phi[1](x) := A*exp(%i*k*x) + B*exp(-%i*k*x);
phi[2](x) := C*exp(rho*x) + D*exp(-rho*x);

current1: -%i*h/m * conjugate(phi[1](x)) * diff(phi[1](x),x,1);
trigsimp( realpart (expand(demoivre(expand(current1)))));
"So there is no interference, and they are essentially j_in - j_out"$

current2: -%i*h/m * conjugate(phi[2](x)) * diff(phi[2](x),x,1);
c2: radcan(realpart(expand(current2)));
"Only interference terms, and this combination is essentially "$
imagpart(conjugate(D)*C);

/*
sol: solve([phi[1](0) = phi[2](0), at(diff(phi[1](x),x,1),x=0) = at(diff(phi[2](x),x,1),x=0)]
          ,[C,D]);
define(phi[2](x), at(phi[2](x), sol[1]));
trigsimp( realpart (expand(demoivre(expand( -%i*h/m * conjugate(phi[2](x)) * diff(phi[2](x),x))))));
*/

"Question 3"$
"a) and b) Dirac-delta potential well."$
assume(h>0,m>0, E<0, k>0, a>0);
"Let me define a wave function, where k:sqrt(2*m*(-E)/h^2)"$
phi[1](x) := A*exp( k*x); /* 0<x */
phi[2](x) := A*exp(-k*x); /* 0<x */
"Let me set an integrated Schroedinger eq, with Dirac delta part on the right:"$
s_eq: at( diff(phi[2](x),x,1) - diff(phi[1](x),x,1), x=0) = 2*m/h^2 * (-a) * phi[1](0);
k_sol:solve(s_eq, k);
solve( ev(k_sol, k: sqrt(2*m*(-E)/h^2)), E);

"
  For the existance condition of bound states, we have
    x*(coth(x)+1) (for odd function), 
    x*(tanh(x)+1) (for even function),
  are equal to some constant (2*m*alpha*a/h^2) as the form.
"$
plot2d(
  [x*(coth(x)+1), x*(tanh(x)+1)],
  [x,0,2],
  [y,0,4],
  [style,[lines,1]
        ,[lines,10]], 
  [gnuplot_preamble, "set key bottom right"]
);
"From this graph, to have a bound state in odd wave function, the constant > 1 is needed."$
