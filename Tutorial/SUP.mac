/* 
Solutions of Univariate Polynomials 

http://maxima.sourceforge.net/docs/tutorial/en/gaertner-tutorial-revision/Pages/ZeroesOfUnivarPol.htm

*/
kill(all);
poly: x^2-x-12;
solutions: solve(poly,x);
"Let us check the solution"$
ev(poly,solutions[1]);
ev(poly,solutions[2]);
"OK, next will be the lambda expression:"$
"We would like to convert these equation-form into term:"$
map(lambda([eq], lhs(eq)-rhs(eq)), solutions);
"We can also define it as a function:"$
fromEq2Term(s) := map(lambda([eq], lhs(eq)-rhs(eq)), s);
terms: fromEq2Term(solutions);
"Let us double-check:"$
apply("*", terms);
expand(%);
"Next, 3rd order polynomial:"$
poly1: x^3 + x^2 - 8*x -12;
sol1: solve(poly1,x);
"From this, we can not reconstruct the original poly:"$
fromEq2Term(sol1);
"There must be double root, but which one?"$
m: multiplicities;
"Here is a generalized version of fromEq2Term:"$
fromEq2Term(s) := 
  map(lambda([eq, exponent], (lhs(eq)-rhs(eq))^exponent)
     , s
     , multiplicities
     );
fromEq2Term(sol1);
expand(apply("*",%));
