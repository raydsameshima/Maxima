/*
Computation of the Reciprocal of an Irrational Number

http://maxima.sourceforge.net/docs/tutorial/en/gaertner-tutorial-revision/Pages/Irrat0001.htm

*/
kill(all)$
"Consider an irrational number:"$
ir1: 1/(2^(1/2) + 3^(1/2));
"This is a number in the field Q[sqrt(2), sqrt(3)], i.e., there is a rep."$
"  a + b*sqrt(2) + c*sqrt(3) + d*sqrt(2)*sqrt(3), a,b,c,d \in Q"$
"ratdisrep... to convert a canonical rational expression (CRE) into a general expression"$
ev(ratdisrep(rat(ir1)), algebraic);
"Let us take another example in Q[sqrt(2),3^(1/3)]"$
ir2: (2^(1/2) + 3^(1/3));
ev(ratdisrep(rat(1/ir2)), algebraic);
%*ir2;
expand(%);
"It works. The third example will be,"$
r: 33^(1/2) * 3;
r1: (17 - r)^(1/3);
r2: (17 + r)^(1/3);
rn: r1 + r2;
1/rn;
ev(ratdisrep(rat(%)), algebraic);
rootscontract(%);
%*rn;
expand(%);
rootscontract(%);
radcan(%), radexpand:all;
"It is convenient to define a function to compute the reciprocal with simplifications:"$
inv(x) :=
block( [r:1/x]
     , rootscontract(ev( ratdisrep(rat(r))
                       , algebraic
                       )));
inv(r1 + r2);
(r1+r2)*inv(r1+r2);
radcan( rootscontract (expand(%))), radexpand:all;
